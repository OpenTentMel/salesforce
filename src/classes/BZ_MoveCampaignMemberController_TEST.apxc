@isTest 
private class BZ_MoveCampaignMemberController_TEST {
    static testMethod void validateRun() {
        Contact campaignOwner = new Contact(FirstName='Test', LastName='CampaignOwner', OwnerId=userInfo.getUserId());
        insert campaignOwner;
        Contact contact = new Contact(FirstName='Test', LastName='User1000');
        insert contact;
        
        // Note: the Campaign.OwnerId refers to the User, so we need to use campaignOwner.OwnerId instead of Id.  See the child relationship of the User object.
        Campaign campaignFrom = BZ_CampaignFactory_TEST.create(campaignOwner.OwnerId, 'Program Participants');
        insert campaignFrom;
        
        CampaignMember cm = new CampaignMember(CampaignId=campaignFrom.Id, ContactId=contact.Id);
        cm.Candidate_Status__c = 'Waitlisted';
        cm.Application_Status__c = 'Submitted';
        insert cm;
        
        Campaign campaignTo = BZ_CampaignFactory_TEST.create(campaignOwner.OwnerId, 'Program Participants');
        insert campaignTo;
        
        Task t1open = new Task(Subject='Test CampaignOpenTask', CampaignMemberId__c=cm.Id, WhoId=contact.Id, WhatId=campaignFrom.Id);
        insert t1open;
        Task t2completed = new Task(Subject='Test CampaignClosedTask', CampaignMemberId__c=cm.Id, WhoId=contact.Id, WhatId=campaignFrom.Id, Status='Completed');
        insert t2completed;
        
    	ApexPages.StandardController sc = new ApexPages.standardController(cm);
            
        PageReference pageRef = Page.BZ_CloseRecruitment;
        Test.setCurrentPage(pageRef);
        
    	BZ_MoveCampaignMemberController controller = new BZ_MoveCampaignMemberController(sc);
        controller.newCampaignLookup.CampaignId = campaignTo.Id;
        PageReference result = controller.run();
        
        System.Assert(result != null, 'controller.run() returned null');
        List<CampaignMember> campaignMembers = [SELECT Id, CampaignId, ContactId, Candidate_Status__c, Application_Status__c FROM CampaignMember WHERE CampaignId = :campaignTo.Id];
        System.Assert(campaignMembers.size() == 1, 'Expected 1 CampaignMember to be moved. Found '+campaignMembers.size());
        CampaignMember updatedCm = campaignMembers.get(0);
        System.Assert(updatedCm.ContactId == cm.ContactId, 'ContactId is wrong, it should be : ' + cm.ContactId + ' instead of: ' + updatedCm.ContactId);
        System.Assert(updatedCm.Candidate_Status__c == cm.Candidate_Status__c, 'Candidate_Status__c should not have been changed to: ' + updatedCm.Candidate_Status__c);
        System.Assert(updatedCm.Application_Status__c == cm.Application_Status__c, 'The Application_Status__c should not have been changed to: ' + updatedCm.Application_Status__c);
        List<Task> tasks = [SELECT Id, WhoId, Subject, EmailTemplate__c FROM Task WHERE WhatId = :campaignTo.Id];
        Boolean foundT1open = false;
        Boolean foundT2completed = false;
        for (Task t : tasks)
        {
            if (t.Subject == t1open.Subject) {
                foundT1open = true;
            }
            if (t.Subject == t2completed.Subject){
                foundT2completed = true;
            }
        }
        System.Assert(foundT1open, 'Task was not copied over: ' + t1open);
        System.Assert(foundT2completed, 'Task was not copied over: ' + t2completed);
        
    }
    
    static testMethod void validateMismatchingCampaignTypes() {
        System.debug('BZ_MoveCampaignMemberController_TEST.validateMismatchingCampaignTypes() - begin');
        Contact campaignOwner = new Contact(FirstName='Test', LastName='CampaignOwner4', OwnerId=userInfo.getUserId());
        insert campaignOwner;
        Contact contact = new Contact(FirstName='Test', LastName='User1004');
        insert contact;
        
        // Note: the Campaign.OwnerId refers to the User, so we need to use campaignOwner.OwnerId instead of Id.  See the child relationship of the User object.
        Campaign campaignFrom = BZ_CampaignFactory_TEST.create(campaignOwner.OwnerId, 'Program Participants');
        campaignFrom.IsActive = true;
        insert campaignFrom;
        
        CampaignMember cm = new CampaignMember(CampaignId=campaignFrom.Id, ContactId=contact.Id);
        cm.Candidate_Status__c = 'Waitlisted';
        cm.Application_Status__c = 'Submitted';
        insert cm;
        
        Campaign campaignTo = BZ_CampaignFactory_TEST.create(campaignOwner.OwnerId, 'Champion');
        campaignTo.IsActive = true;
        insert campaignTo;
                
    	ApexPages.StandardController sc = new ApexPages.standardController(cm);
            
        PageReference pageRef = Page.BZ_CloseRecruitment;
        Test.setCurrentPage(pageRef);
        
    	BZ_MoveCampaignMemberController controller = new BZ_MoveCampaignMemberController(sc);
        controller.newCampaignLookup.CampaignId = campaignTo.Id;
        System.debug('BZ_MoveCampaignMemberController_TEST.validateMismatchingCampaignTypes() - controller.run()');
        PageReference result = controller.run();
        
        System.Assert(result == null, 'controller.run() should return null');
        System.Assert(ApexPages.hasMessages(), 'An error message should be added to the page b/c campaigns of different Types cant be moved between.');
    }
    
    static testMethod void validatePreAcceleratorAndFellowCampaignTypes() {
        System.debug('BZ_MoveCampaignMemberController_TEST.validatePreAcceleratorAndFellowCampaignTypes() - begin');
        Contact campaignOwner = new Contact(FirstName='Test', LastName='CampaignOwner5', OwnerId=userInfo.getUserId());
        insert campaignOwner;
        Contact contact = new Contact(FirstName='Test', LastName='User1005');
        insert contact;
        
        // Note: the Campaign.OwnerId refers to the User, so we need to use campaignOwner.OwnerId instead of Id.  See the child relationship of the User object.
        Campaign campaignFrom = BZ_CampaignFactory_TEST.create(campaignOwner.OwnerId, 'Pre-Accelerator Participants');
        campaignFrom.IsActive = true;
        insert campaignFrom;
        
        CampaignMember cm = new CampaignMember(CampaignId=campaignFrom.Id, ContactId=contact.Id);
        cm.Candidate_Status__c = 'Accepted';
        cm.Application_Status__c = 'Submitted';
        insert cm;
        
        Campaign campaignTo = BZ_CampaignFactory_TEST.create(campaignOwner.OwnerId, 'Program Participants');
        campaignTo.IsActive = true;
        insert campaignTo;
                
    	ApexPages.StandardController sc = new ApexPages.standardController(cm);
            
        PageReference pageRef = Page.BZ_CloseRecruitment;
        Test.setCurrentPage(pageRef);
        
    	BZ_MoveCampaignMemberController controller = new BZ_MoveCampaignMemberController(sc);
        controller.newCampaignLookup.CampaignId = campaignTo.Id;
        System.debug('BZ_MoveCampaignMemberController_TEST.validatePreAcceleratorAndFellowCampaignTypes() - controller.run()');
        PageReference result = controller.run();
        
        System.Assert(result != null, 'controller.run() returned null which indicates failure when this should work');
        List<CampaignMember> campaignMembers = [SELECT Id, CampaignId, ContactId, Candidate_Status__c, Application_Status__c FROM CampaignMember WHERE CampaignId = :campaignTo.Id];
        System.Assert(campaignMembers.size() == 1, 'Expected 1 CampaignMember to be moved. Found '+campaignMembers.size());
        CampaignMember updatedCm = campaignMembers.get(0);
        System.Assert(updatedCm.ContactId == cm.ContactId, 'ContactId is wrong, it should be : ' + cm.ContactId + ' instead of: ' + updatedCm.ContactId);
        System.Assert(updatedCm.Candidate_Status__c == cm.Candidate_Status__c, 'Candidate_Status__c should not have been changed to: ' + updatedCm.Candidate_Status__c);
        System.Assert(updatedCm.Application_Status__c == cm.Application_Status__c, 'The Application_Status__c should not have been changed to: ' + updatedCm.Application_Status__c);
    }
}
